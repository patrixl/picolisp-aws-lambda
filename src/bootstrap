#!./pil/bin/picolisp ./pil/lib.l

(load "@lib/misc.l" "@lib/http.l")

(setq *Handler (sys "_HANDLER")
   *Region  (sys "AWS_REGION")
   *AwsExecutionEnv  (sys "AWS_EXECUTION_ENV")
   *AwsFunctionName  (sys "AWS_LAMBDA_FUNCTION_NAME")
   *AwsLambdaFunctionMemorySize  (sys "AWS_LAMBDA_FUNCTION_MEMORY_SIZE")
   *AwsLambdaFunctionVersion  (sys "AWS_LAMBDA_FUNCTION_VERSION")
   *AwsLogGroupName  (sys "AWS_LAMBDA_LOG_GROUP_NAME")
   *AwsLogStreamName  (sys "AWS_LAMBDA_LOG_STREAM_NAME")
   *AwsAccessKeyId  (sys "AWS_ACCESS_KEY_ID")
   *AwsSecretAccessKey  (sys "AWS_SECRET_ACCESS_KEY")
   *AwsSessionToken  (sys "AWS_SESSION_TOKEN")
   *LambdaTaskRoot  (sys "LAMBDA_TASK_ROOT")
   *LambdaRuntimeDir  (sys "LAMBDA_RUNTIME_DIR")
   *AwsLambdaRuntimeApi  (sys "AWS_LAMBDA_RUNTIME_API")
   *Tz  (sys "TZ")

   *Lang (sys "LANG")
   *Path (sys "PATH")
   *LdLibraryPath (sys "LD_LIBRARY_PATH")

   *RequestId NIL)

(de *Err
   (prinl "Error at: " (str ^))
   (let (Curl (make (link 'curl "--silent" "--show-error" "--location" "-X" "POST"
                       (pack "http://" *AwsLambdaRuntimeApi "/2018-06-01/runtime/invocation/" *RequestId "/error")
                       "-d" (pack "{'errorMessage': 'Error at: " (str ^)
                               "', 'errorType': 'Runtime'}" ) "--header" "Lambda-Runtime-Function-Error-Type: Runtime" ) ) )
      (in Curl (till NIL T)) ) )

(loop
   (cd *LambdaTaskRoot)

   (let (Script (car (mapcar pack (split (chop *Handler) ".")))
         Function (cadr (mapcar pack (split (chop *Handler) ".")))
         Temp (in '(mktemp) (till "^J" T))
         Url (pack "http://" *AwsLambdaRuntimeApi "/2018-06-01/runtime/invocation/next")
         Curl (make (link 'curl "--silent" "--show-error" "--location" "--dump-header" Temp
                       "-X" "GET" Url ) )
         EventData (in Curl (till NIL T))
         Headers (mapcar '((L) (mapcar '((A) (pack (clip A))) (split L ":"))) (split (chop (in Temp (till NIL T))) "^J")) )

      (setq *RequestId (cadr (assoc "Lambda-Runtime-Aws-Request-Id" Headers)) )

      (finally
         (call 'rm Temp)

         (load (pack *LambdaTaskRoot "/" Script ".l"))
         (setq Response (eval (append (str (pack Function)) (list EventData Headers))))

         (setq Url (pack "http://" *AwsLambdaRuntimeApi "/2018-06-01/runtime/invocation/" *RequestId "/response"))
         (setq Curl (make (link 'curl "--silent" "--show-error" "--location" "-X" "POST" Url "-d" Response)))
         (in Curl (till NIL T)) ) ) )

(bye)
